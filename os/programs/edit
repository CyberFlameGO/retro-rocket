max = 100
lines = 0
top = 0
currentx = 0
currenty = 0
DIM content$, max
PROCload(ARGS$)
PROCdraw
PROCtext
PROCedit
END
DEF PROCdraw
	CLS
	BACKGROUND 4
	COLOUR 15
	CURSOR 1, TERMHEIGHT
	FOR X = 0 TO TERMWIDTH
		PRINT " ";
	NEXT
	CURSOR 2, TERMHEIGHT
	PRINT "ESC EXIT"; CHR$(9); "^S SAVE"; CHR$(9); CHR$(9); "^W WHEREIS"; CHR$(9); "^R REPLACE";
	CURSOR 1, 0
	FOR X = 0 TO TERMWIDTH
		PRINT " ";
	NEXT
	CURSOR TERMWIDTH / 2 - 2, 0
	PRINT "EDIT"
	COLOUR 7
	BACKGROUND 0
	CURSOR 1, 2
RETPROC
DEF PROCtext
	CURSOR 1, 2
	FOR l = top TO top + TERMHEIGHT - 2
		PRINT CHR$(27) + "[0K";
		IF l < max THEN PRINT LEFT$(content$(l), TERMWIDTH)
	NEXT
RETPROC
DEF PROCload(file$)
	FH = OPENIN(file$)
	IF FH < 0 THEN PROCfail
	lines = 0
	REPEAT
		content$(lines) = READ$(FH)
		lines = lines + 1
		IF lines = max THEN PROCgrow
	UNTIL EOF(FH) = 1
	CLOSE FH
RETPROC
DEF PROCgrow
	max = max + 100
	REDIM content$, max
RETPROC
DEF PROCfail
	PRINT "Could not open file: "; ARGS$
	END
RETPROC
DEF PROCedit
	REPEAT
		CURSOR currentx + 1, currenty + 2
		key$ = INKEY$
		IF key$ > "" THEN PROCprocessKey(ASC(key$))
	UNTIL FALSE
RETPROC
DEF PROCprocessKey(in)
	IF in = 27 THEN PROCquit
	IF in = 250 THEN PROCup
	IF in = 251 THEN PROCdown
	IF in = 252 THEN PROCleft
	IF in = 253 THEN PROCright
	IF in < 128 THEN PRINT CHR$(in);
RETPROC
DEF PROCquit
	CLS
	END
RETPROC
DEF PROCleft
	currentx = currentx - 1
	IF currentx < 0 THEN currentx = 0
RETPROC
DEF PROCup
	currenty = currenty - 1
	IF currenty < 0 THEN PROCscrollUp
RETPROC
DEF PROCdown
	currenty = currenty + 1
	IF currenty > TERMHEIGHT - 3 THEN PROCscrollDown
RETPROC
DEF PROCscrollDown
	currenty = TERMHEIGHT - 3
	top = top + 1
	PROCtext
RETPROC
DEF PROCscrollUp
	currenty = 0
	top = top - 1
	IF top < 0 THEN top = 0
	PROCtext
RETPROC
DEF PROCright
	currentx = currentx + 1
	IF currentx > TERMWIDTH - 1 THEN currentx = TERMWIDTH - 1
RETPROC
